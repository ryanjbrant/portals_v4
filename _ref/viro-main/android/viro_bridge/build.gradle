apply plugin: 'com.android.library'
apply plugin: "kotlin-android"
apply plugin: "com.facebook.react"

// Check if the new architecture is enabled
def isNewArchitectureEnabled() {
    return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

android {
    compileSdkVersion 34
    namespace "com.viromedia.bridge"
    
    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        
        // Add build config for New Architecture detection
        buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
    
    // Add C++ build for JSI bridge - Enable when CMake is available
    // To enable JSI bridge compilation, uncomment the following block:
    /*
    if (isNewArchitectureEnabled()) {
        externalNativeBuild {
            cmake {
                path "src/main/cpp/CMakeLists.txt"
                version "3.13.0"
            }
        }
        
        defaultConfig {
            externalNativeBuild {
                cmake {
                    cppFlags "-std=c++17", "-fexceptions", "-frtti", "-DWITH_INSPECTOR=1"
                    abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
                    arguments "-DANDROID_STL=c++_shared",
                              "-DREACT_NATIVE_DIR=${rootProject.projectDir}/../node_modules/react-native",
                              "-DNODE_MODULES_DIR=${rootProject.projectDir}/../node_modules"
                }
            }
        }
    }
    */
}

react {
    // Configure node executable with full path to avoid PATH issues
    def nodeExec = project.hasProperty('nodeExecutable') ? project.property('nodeExecutable') : null
    if (nodeExec != null && !nodeExec.isEmpty()) {
        nodeExecutableAndArgs.set([nodeExec])
    } else {
        // Try to find node in common locations as fallback
        def nodePath = ["/usr/local/bin/node", "/usr/bin/node",
                        System.getProperty("user.home") + "/.local/share/fnm/node-versions/v20.19.5/installation/bin/node"]
                        .find { new File(it).exists() }
        if (nodePath != null) {
            nodeExecutableAndArgs.set([nodePath])
        }
    }
}

// Also configure the tasks directly to ensure the setting is applied
// This is necessary because the React plugin creates tasks before the react {} block is evaluated
afterEvaluate {
    def nodeExec = project.hasProperty('nodeExecutable') ? project.property('nodeExecutable') : null
    if (nodeExec != null && !nodeExec.isEmpty()) {
        // Configure all React Native codegen tasks with the full path to node
        tasks.withType(com.facebook.react.tasks.GenerateCodegenSchemaTask).configureEach {
            nodeExecutableAndArgs.set([nodeExec])
        }
        tasks.withType(com.facebook.react.tasks.GenerateCodegenArtifactsTask).configureEach {
            nodeExecutableAndArgs.set([nodeExec])
        }
    }
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.6.0"

    implementation("com.facebook.react:react-android:0.76.0")
    //implementation("com.facebook.react:react-native:+")

    implementation 'androidx.appcompat:appcompat:1.0.0'
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.ar:core:1.43.0'
    implementation project(':gvr_common')
    implementation project(':viro_renderer')
    implementation 'com.google.android.exoplayer:exoplayer:2.19.1'
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.1.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation "com.facebook.fresco:fresco:3.1.3"
    implementation "com.facebook.fresco:imagepipeline:3.1.3"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:3.1.3"
    implementation 'com.facebook.fresco:imagepipeline-native:3.1.3'
    implementation 'com.facebook.fresco:memory-type-ashmem:3.1.3'
    implementation "com.facebook.fresco:ui-common:3.1.3"
    implementation "com.facebook.fresco:middleware:3.1.3"
    implementation "com.facebook.fresco:animated-gif:3.1.3"
    implementation 'com.facebook.soloader:nativeloader:0.10.5'
    implementation 'com.facebook.fresco:fbcore:3.1.3'
    implementation 'com.facebook.fresco:drawee:3.1.3'
    implementation 'com.facebook.fresco:memory-type-native:3.1.3'
    implementation 'com.facebook.fresco:memory-type-java:3.1.3'
    implementation 'com.facebook.fresco:nativeimagefilters:3.1.3'
    implementation 'com.facebook.fresco:nativeimagetranscoder:3.1.3'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
